<& /Admin/Elements/Header, Title => $title &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@results &>

<form method="post" enctype="multipart/form-data" action="<% RT->Config->Get('WebPath') %>/Admin/Tools/AnnounceSimple.html">
<input type="hidden" class="hidden" name="Queue" value="<% $Queue %>" />

<input type="text" name="announce" style="width: 50%;" value="<% $announce %>" />

<div>
% if ($attr) {
<input type="submit" name="delete" value="<&|/l&>Delete</&>" />
% }
<input type="submit" name="save" value="<&|/l&>Save</&>" />
</div>

</form>
<%init>
unless ($session{'CurrentUser'}->HasRight( Object=> RT->System, Right => 'SuperUser')) {
    Abort(loc('This feature is only available to system administrators.'));
}

my @results;
my $title;

my $object;
if ($Queue) {
    $object = RT::Queue->new( $session{'CurrentUser'} );
    $object->Load( $Queue );
    Abort( loc("Couldn't load queue [_1]", $Queue) )
        unless $object->id;
    $title = loc('Announce for Queue [_1]', $object->Name);
} else {
    $object = RT::System->new( $session{CurrentUser} );
    $title = loc('Global Announce');
}

if ( $ARGS{'save'} and length $announce ) {
    my ($status, $msg) = $object->SetAttribute(
        Name        => 'AnnounceSimple',
        Description => 'AnnounceSimple text',
        Content     => $announce,
    );

    push @results, loc("Unable to update Announce text: [_1]", $msg)
        unless $status;
} elsif ( $ARGS{'delete'} ) {
    my ($status, $msg) = $object->DeleteAttribute('AnnounceSimple');

    push @results, loc("Unable to delete Announce text: [_1]", $msg)
        unless $status;
}

my $attr = $object->FirstAttribute('AnnounceSimple');
$announce = $attr ? $attr->Content : '';
</%init>
<%args>
$announce => ''
$Queue => 0
</%args>
